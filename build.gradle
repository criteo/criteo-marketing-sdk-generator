buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.4"
    }
}

apply plugin: 'java-library'
apply plugin: 'org.openapi.generator'

ext {
    artifactId = "python-client-generator"
    groupId = "com.criteo.mapi"
    version = '1.0.0'
    description = 'Criteo Generator for Python Client'
    sourceCompatibility = '1.8'
    jar.enabled = false
}

def pythonOutputDir = "$projectDir/dist".toString()

task cleanPreviousDist(type: Delete) {
    group 'Criteo'
    description 'Clean up dist folder'

    delete pythonOutputDir
    followSymlinks = true
}

task copyExamples(type: Copy) {
    group 'Criteo'
    description 'Copy the Python examples into the dist folder'

    from 'resources/python/examples'
    into "$pythonOutputDir/examples".toString()
}

task copyLicense(type: Copy) {
    group 'Criteo'
    description 'Copy the license into the dist folder'

    from 'LICENSE'
    into pythonOutputDir
    rename 'LICENSE', 'LICENSE.txt'
}

task copyTests(type: Copy) {
    group 'Criteo'
    description 'Copy the Python tests into the dist folder'

    from 'resources/python/tests/'
    into "$pythonOutputDir/test/".toString()
}

task removeUnwantedFiles(type: Delete) {
    group 'Criteo'
    description 'Remove files generated by openapi-generator that we don\'t to push'

    delete fileTree(pythonOutputDir) {
        include '.openapi-generator-ignore', '.travis.yml', 'git_push.sh'
    }
    followSymlinks = true
}

task buildPythonClient(type: GradleBuild) {
    group 'Criteo'
    description 'Generate the Python client using openapi-generator and custom templates'

    tasks = ['cleanPreviousDist', 'openApiGenerate', 'copyTests', 'copyExamples', 'copyLicense', 'removeUnwantedFiles']
}

def apiVersion = '1.0'
def swaggerURL = 'https://api.criteo.com/marketing/swagger/docs/v.' + apiVersion
def clientPatch = hasProperty('buildNumber') ? buildNumber : '0'

openApiGenerate {
    generatorName = "python"
    templateDir = "$projectDir/templates/PythonCriteo".toString()
    inputSpec = swaggerURL
    outputDir = pythonOutputDir
    removeOperationIdPrefix = true
    generateApiTests = false
    generateModelTests = false
    configOptions = [
            packageName            : 'criteo_marketing',
            projectName            : 'criteo_marketing',
            packageUrl             : 'https://github.com/criteo/criteo-python-marketing-sdk',
            packageVersion         : "$apiVersion.$clientPatch".toString(),
            hideGenerationTimestamp: 'true'
    ]
}