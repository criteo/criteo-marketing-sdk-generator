buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.0.0-beta2"
    }
}

apply plugin: 'org.openapi.generator'

def clientOutputDir = "$projectDir/../$rootProject.gereratedClientsDirName/java".toString()
def resourcesDir = "$projectDir/resources".toString()
def basePackage = "com.criteo.marketing"
def basePackagePath = basePackage.replace(".", "/")

task cleanPreviousJavaBuild(type: Delete) {
    group 'Criteo'
    description 'Clean up generated client output folder'

    delete clientOutputDir
    followSymlinks = true
}

task copyExamples(type: Copy) {
    from "$resourcesDir/examples"
    into "$clientOutputDir/src/examples/java/$basePackagePath/".toString()
}

task copyTests(type: Copy) {
    from "$resourcesDir/tests/"
    into "$clientOutputDir/src/test/java/$basePackagePath/".toString()
}

task generateClient(type: GradleBuild) {
    group "Criteo"
    description 'Generate the Java client using openapi-generator and custom templates'

    tasks = ['cleanPreviousJavaBuild', 'openApiGenerate', 'copyExamples', 'copyTests']
}

def clientPatch = hasProperty('buildNumber') ? buildNumber : '0'

openApiGenerate {
    generatorName = "java"
    templateDir = "$resourcesDir/templates/".toString()
    inputSpec = rootProject.swaggerURL
    outputDir = clientOutputDir
    removeOperationIdPrefix = true
    generateApiTests = false
    generateModelTests = false
    configOptions = [
            groupId                  : 'com.criteo',
            artifactId               : 'marketing.java-client',
            apiPackage               : "${basePackage}.api".toString(),
            modelPackage             : "${basePackage}.model".toString(),
            artifactUrl              : 'https://github.com/criteo/criteo-java-marketing-sdk',
            artifactDescription      : 'Criteo Java Marketing SDK',
            scmConnection            : 'scm:git:git@github.com:criteo/criteo-java-marketing-sdk.git',
            scmDeveloperConnection   : 'scm:git:git@github.com:criteo/criteo-java-marketing-sdk.git',
            scmUrl                   : 'https://github.com/criteo/criteo-java-marketing-sdk',
            developerName            : 'Criteo',
            developerEmail           : '',
            developerOrganization    : 'Criteo',
            developerOrganizationUrl : 'https://www.criteo.com/',
            licenseName              : 'Apache License, version 2.0',
            licenseUrl               : 'https://www.apache.org/licenses/LICENSE-2.0',
            hideGenerationTimestamp  : 'true',
            packageVersion           : "$apiVersion.$clientPatch".toString(),
            dateLibrary              : 'java8',
    ]
}
