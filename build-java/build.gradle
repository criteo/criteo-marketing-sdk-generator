buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.0.0-beta2"
    }
}

apply plugin: 'org.openapi.generator'

def clientOutputDir = "$projectDir/../$rootProject.gereratedClientsDirName/java".toString()
def resourcesDir = "$projectDir/resources".toString()
def basePackage = "com.criteo.marketing"
def basePackagePath = basePackage.replace(".", "/")

task cleanPreviousJavaBuild(type: Delete) {
    group 'Criteo'
    description 'Clean up generated client output folder'

    delete clientOutputDir
    followSymlinks = true
}

task copyLicense(type: Copy) {
    group 'Criteo'
    description 'Copy the license into the client output folder'

    from "$projectDir/../LICENSE"
    into clientOutputDir
    rename 'LICENSE', 'LICENSE.txt'
}

task copyExamples(type: Copy) {
    from "$resourcesDir/examples"
    into "$clientOutputDir/src/examples/java/$basePackagePath/".toString()
}

task copyTests(type: Copy) {
    from "$resourcesDir/tests/"
    into "$clientOutputDir/src/test/java/$basePackagePath/".toString()
}

task removeUnwantedFiles(type: Delete) {
    group 'Criteo'
    description 'Remove files generated by openapi-generator that we don\'t want to push'

    delete fileTree(clientOutputDir) {
        include '.openapi-generator-ignore', '.travis.yml', 'git_push.sh',\
                'build.sbt', 'src/main/AndroidManifest.xml', 'gradle.properties'
    }
    followSymlinks = true
}

task generateClient(type: GradleBuild) {
    group "Criteo"
    description 'Generate the Java client using openapi-generator and custom templates'

    tasks = ['cleanPreviousJavaBuild', 'openApiGenerate', 'copyExamples', 'copyTests', 'copyLicense', 'removeUnwantedFiles']
}

def clientPatch = hasProperty('buildNumber') ? buildNumber : '0'

openApiGenerate {
    generatorName = "java"
    templateDir = "$resourcesDir/templates/".toString()
    inputSpec = rootProject.swaggerURL
    outputDir = clientOutputDir
    removeOperationIdPrefix = true
    generateApiTests = false
    generateModelTests = false
    configOptions = [
            groupId                  : 'com.criteo',
            apiPackage               : "${basePackage}.api".toString(),
            artifactId               : 'marketing.java-client',
            artifactVersion          : "$apiVersion.$clientPatch".toString(),
            artifactDescription      : 'Criteo Marketing SDK for Java',
            artifactUrl              : 'https://github.com/criteo/criteo-java-marketing-sdk',
            modelPackage             : "${basePackage}.model".toString(),
            scmConnection            : 'scm:git:git://github.com/criteo/criteo-java-marketing-sdk.git',
            scmDeveloperConnection   : 'scm:git:ssh://github.com:criteo/criteo-java-marketing-sdk.git',
            scmUrl                   : 'https://github.com/criteo/criteo-java-marketing-sdk',
            developerName            : 'Criteo',
            developerEmail           : 'open-source@criteo.com',
            developerOrganization    : 'Criteo',
            developerOrganizationUrl : 'https://www.criteo.com/',
            licenseName              : 'Apache License, version 2.0',
            licenseUrl               : 'https://www.apache.org/licenses/LICENSE-2.0.txt',
            hideGenerationTimestamp  : 'true',
            dateLibrary              : 'java8',
    ]
}
